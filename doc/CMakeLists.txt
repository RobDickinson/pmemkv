# SPDX-License-Identifier: BSD-3-Clause
# Copyright 2019-2020, Intel Corporation

function(strip_example PATH VAL)
	# Prepare example code to inject into man page.
	# Read file pointed by ${PATH}, remove comments (with the license
	# and the file description) up to the first preprocessor's directive,
	# and place it in ${VAL} in a caller scope.
	file(READ
		${PATH}
		example_content)
	string(REGEX REPLACE "([/]+[*/]+).*([*]+[/]+)([\n]+#)" "#"
		example_content "${example_content}")
	set(${VAL} "${example_content}" PARENT_SCOPE)
endfunction()

# Converts md files into manpage format
# NAME is output manpage name, INPUT is a path to source md file
# Requires pandoc.
function(configure_man NAME INPUT)
	add_custom_command(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/man/${NAME}
		MAIN_DEPENDENCY ${INPUT}
		COMMAND ${PMEMKV_ROOT_DIR}/utils/md2man/md2man.sh
			${INPUT}
			${PMEMKV_ROOT_DIR}/utils/md2man/default.man
			${CMAKE_CURRENT_BINARY_DIR}/man/${NAME}
			${VERSION})
endfunction()

add_check_whitespace(man ${CMAKE_CURRENT_SOURCE_DIR}/*.*)

# prepare C documentation (using manpage format)
find_program(PANDOC NAMES pandoc)
if(PANDOC)
	strip_example(${PMEMKV_ROOT_DIR}/examples/pmemkv_basic_c/pmemkv_basic.c C_EXAMPLE)
	configure_file(${CMAKE_CURRENT_SOURCE_DIR}/libpmemkv.3.md.in
		${CMAKE_CURRENT_BINARY_DIR}/man/tmp/libpmemkv.3.md)

	strip_example(
		${PMEMKV_ROOT_DIR}/examples/pmemkv_config_c/pmemkv_config.c
		CONFIG_TYPE_BASED_EXAMPLE)
	strip_example(
		${PMEMKV_ROOT_DIR}/examples/pmemkv_config_c/pmemkv_basic_config.c
		CONFIG_BASIC_EXAMPLE)
	configure_file(${CMAKE_CURRENT_SOURCE_DIR}/libpmemkv_config.3.md.in
		${CMAKE_CURRENT_BINARY_DIR}/man/tmp/libpmemkv_config.3.md)

	strip_example(
		${PMEMKV_ROOT_DIR}/examples/pmemkv_transaction_c/pmemkv_transaction.c
		TRANSACTION_BASIC_C_EXAMPLE)
	configure_file(${CMAKE_CURRENT_SOURCE_DIR}/libpmemkv_tx.3.md.in
		${CMAKE_CURRENT_BINARY_DIR}/man/tmp/libpmemkv_tx.3.md)

	strip_example(
		${PMEMKV_ROOT_DIR}/examples/pmemkv_iterator_c/pmemkv_iterator.c
		ITERATOR_BASIC_EXAMPLE)
	configure_file(${CMAKE_CURRENT_SOURCE_DIR}/libpmemkv_iterator.3.md.in
		${CMAKE_CURRENT_BINARY_DIR}/man/tmp/libpmemkv_iterator.3.md)

	# convert md files to manpage format
	configure_man(libpmemkv.7 ${CMAKE_CURRENT_SOURCE_DIR}/libpmemkv.7.md)
	configure_man(libpmemkv.3 ${CMAKE_CURRENT_BINARY_DIR}/man/tmp/libpmemkv.3.md)
	configure_man(libpmemkv_config.3 ${CMAKE_CURRENT_BINARY_DIR}/man/tmp/libpmemkv_config.3.md)
	configure_man(libpmemkv_json_config.3 ${CMAKE_CURRENT_SOURCE_DIR}/libpmemkv_json_config.3.md)
	configure_man(libpmemkv_tx.3 ${CMAKE_CURRENT_BINARY_DIR}/man/tmp/libpmemkv_tx.3.md)
	configure_man(libpmemkv_iterator.3 ${CMAKE_CURRENT_BINARY_DIR}/man/tmp/libpmemkv_iterator.3.md)

	# install manpages
	install(FILES ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv.7
		DESTINATION ${CMAKE_INSTALL_MANDIR}/man7)
	install(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/man/
		DESTINATION ${CMAKE_INSTALL_MANDIR}/man3
		FILES_MATCHING
		PATTERN "*.3"
		PATTERN "tmp" EXCLUDE)
else()
	message(WARNING "pandoc not found - man pages will not be generated")
endif()

# prepare C++ documentation (using doxygen format)
include(FindDoxygen)
if(DOXYGEN_FOUND AND DOXYGEN_DOT_FOUND)
	if(DEVELOPER_MODE)
		set(DOXYGEN_WARN_AS_ERROR "YES")
	else()
		set(DOXYGEN_WARN_AS_ERROR "NO")
	endif()

	configure_file("${CMAKE_CURRENT_SOURCE_DIR}/libpmemkv.Doxyfile.in"
		"${CMAKE_CURRENT_BINARY_DIR}/libpmemkv.Doxyfile" @ONLY)
	install(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/cpp_html/ DESTINATION ${CMAKE_INSTALL_DOCDIR})
elseif(NOT DOXYGEN_FOUND)
	message(WARNING "Doxygen not found - Doxygen documentation will not be generated")
else()
	message(WARNING "Dot tool not found - Doxygen documentation will not be generated")
endif()

# set 'doc' target for make
if(PANDOC AND DOXYGEN_FOUND AND DOXYGEN_DOT_FOUND)
	add_custom_target(doc ALL
		${DOXYGEN_EXECUTABLE} "${CMAKE_CURRENT_BINARY_DIR}/libpmemkv.Doxyfile"
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv.7
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv.3
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv_config.3
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv_json_config.3
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv_tx.3
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv_iterator.3
		WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
elseif(PANDOC)
	add_custom_target(doc ALL
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv.7
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv.3
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv_config.3
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv_json_config.3
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv_tx.3
		DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/libpmemkv_iterator.3
		WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
elseif(DOXYGEN_FOUND AND DOXYGEN_DOT_FOUND)
	add_custom_target(doc ALL
		${DOXYGEN_EXECUTABLE} "${CMAKE_CURRENT_BINARY_DIR}/libpmemkv.Doxyfile"
		WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
endif()
